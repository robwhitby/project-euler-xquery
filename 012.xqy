xquery version "1.0";

declare namespace math="java:java.lang.Math";

(:
The sequence of triangle numbers is generated by adding the natural numbers. 
So the 7th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 = 28. The first ten terms would be:

1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...

Let us list the factors of the first seven triangle numbers:

 1: 1
 3: 1,3
 6: 1,2,3,6
10: 1,2,5,10
15: 1,3,5,15
21: 1,3,7,21
28: 1,2,4,7,14,28
We can see that 28 is the first triangle number to have over five divisors.

What is the value of the first triangle number to have over five hundred divisors?
:)

declare function local:num-factors($n)
{
  let $sqrt := xs:long(math:sqrt($n))
  return fn:sum((
    fn:count((1 to $sqrt)[$n mod . eq 0]) * 2,
    if ($sqrt * $sqrt eq $n) then -1 else 0
  ))
};

declare function local:triangles($triangle, $n, $divisors) {
  let $tnext := $triangle + $n
  return
    if (local:num-factors($tnext) gt $divisors) then $tnext
    else local:triangles($tnext, $n + 1, $divisors)
};

local:triangles(1,2,500)
